plugins {
    id 'org.springframework.boot' version '2.2.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'groovy'
    id 'org.unbroken-dome.test-sets' version '2.2.1'
    id 'info.solidsoft.pitest' version '1.4.6'

}


group = 'com.splitoil'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 12
targetCompatibility = 12
def cucumberVersion = '4.7.4'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //groovy
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.8'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    //postgresql
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.9'

    //spring
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.2.2.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '2.2.2.RELEASE'
    testCompile group: 'org.springframework.security', name: 'spring-security-test', version: '5.3.0.RELEASE'

    //spock
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
    testCompile group: 'org.spockframework', name: 'spock-spring', version: '1.3-groovy-2.5'

    //cucumber
    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"

    //jackson
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.1'

    //apache commons
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'

    //arch unit test
    testCompile 'com.tngtech.archunit:archunit:0.12.0'

    //HATEOAS
    compile group: 'org.springframework.hateoas', name: 'spring-hateoas', version: '1.0.2.RELEASE'

}

testSets {
    integrationTest
    test
}

test {
    useJUnit {
        includeCategories 'com.splitoil.UnitTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

integrationTest {
    useJUnit {
        includeCategories 'com.splitoil.IntegrationTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

task cucumber() {
    dependsOn assemble, compileIntegrationTestGroovy
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.integrationTest.output
            args = ['--plugin', 'pretty', '--glue', 'classpath:com.splitoil', 'src/integrationTest/resources/cucumber']
        }
    }
}

// Mutation tests
pitest {
    targetClasses = ['com.splitoil.gasstation.domain.*', 'com.splitoil.car.domain.*']
    mutators = ['DEFAULTS', 'EMPTY_RETURNS', 'NULL_RETURNS']
}